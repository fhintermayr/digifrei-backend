plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.3'
	id 'io.spring.dependency-management' version '1.1.0'
	id "org.openapi.generator" version "6.4.0"
}

group = 'de.icp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation "io.swagger.core.v3:swagger-annotations:2.2.9"
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "spring"
	apiPackage = "de.icp.match.api"
	modelPackage = "de.icp.match.dto"
	modelNameSuffix = "Dto"

	inputSpec = "$rootDir/src/main/resources/openapi.yaml"
	outputDir = "$buildDir/generated"

	generateModelTests = false
	generateApiTests = false

	configOptions = [
			interfaceOnly: "true",
			skipDefaultInterface: "true",
			hideGenerationTimestamp: "true",
			useSpringBoot3: "true",
			openApiNullable: "false"
	]
}

/* The interfaces which are generated from the api specification are generated to the build/generated directory
	In order to get access to them from the main class path, I have created a source set which logically links the
	main classpath with the build/generated class path
 */
sourceSets.main.java.srcDirs = ['build/generated/src/main/java','src/main/java']

compileJava.dependsOn tasks.openApiGenerate